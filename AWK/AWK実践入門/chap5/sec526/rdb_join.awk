##====================================================================
##  成績表
##  regist.awk
##====================================================================

##--------------------------------------------------------------------
##  事前処理
##--------------------------------------------------------------------

BEGIN {
    FS = ",";    # CSV形式への対応
}


##--------------------------------------------------------------------
##  本体処理
##--------------------------------------------------------------------

FILENAME == "student.csv" && FNR > 1  {
    ##----  学生情報の読込み
    stud_info[$1]["facu"] = $2;     # 学科
    stud_info[$1]["grade"] = $3;    # 学年
}

FILENAME == "subject.csv" && FNR > 1  {
    ##----  科目情報の読込み
    subj_info[$1]["tutor"] = $2;    # 担当
    subj_info[$1]["week"] = $3;     # 週
    subj_info[$1]["hour"] = $4;     # 時
}

FILENAME == "regist.csv" {
    ##----  受講情報の読込み
    FS = ",";
    regist[$1][$2] = $3;
}


##--------------------------------------------------------------------
##  事後処理
##--------------------------------------------------------------------

END  {
    ##----  学生名と科目名
    print "学生名と科目名";
    for ( stud in stud_info ) {
        for ( subj in subj_info ) {
            if ( regist[stud][subj] == "" ) { continue; }    # 受講無
            print stud " " subj;                             # 出力
        }
    }
    print ""

    ##----  学生名と担当者
    print "学生名と担当者";
    for ( stud in stud_info ) {
        for ( subj in subj_info ) {
            if ( regist[stud][subj] == "" ) { continue; }    # 受講無
            tutor = subj_info[subj]["tutor"];                # 担当者
            if ( teach[stud][tutor] != "" ) { continue; }    # 既出
            teach[stud][tutor] = 1;                          # 初出
            print stud " " tutor;                            # 出力
        }
    }
}
